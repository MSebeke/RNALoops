#!/usr/bin/env perl

sub getPath {
	my ($url) = @_;
	my @parts = split(m|/|, $url);
	pop @parts;
	unshift @parts, "./" if (@parts == 0);
	return join('/', @parts).'/';
}

use lib getPath($0)."../lib/";

use strict;
use warnings;
use Data::Dumper;
use Getopt::Long;
use foldGrammars::Utils;
use POSIX 'isatty';

our $MODE_MFE = 'mfe';
our $MODE_SUBOPT = 'subopt';
our $MODE_ENFORCE = 'enforce';
our $MODE_LOCAL = 'local';
our $MODE_SHAPES = 'shapes';
our $MODE_PROBS = 'probs';

my %PARAM;
$PARAM{mode} = {key => 'mode', type => 's', default => $MODE_SUBOPT, info => "Select the computation mode. Available modes are \"$MODE_MFE\", \"$MODE_SUBOPT\", \"$MODE_ENFORCE\", \"$MODE_LOCAL\", \"$MODE_SHAPES\", \"$MODE_PROBS\". Omit the ticks on input.\nDefault is \"@(DEFAULT)\"."};
$PARAM{windowsize} = {key => 'windowSize', type => 'i', gapc => 'w', default => undef, info => "Activates window mode and computes substrings of size <int> for the input. After computation for the first <int> bases is done, the window is pushed <y> bases to the right and the next computation is startet. <y> is set by --@(windowincrement).\n<int> must be a non-zero positive integer, smaller than the input length."};
$PARAM{windowincrement} = {key => 'windowIncrement', gapc => 'i', type => 'i', default => 1, info => "If --@(windowsize) is given, this parameter sets the offset for the next window to <int> bases.\n<int> must be a non-zero positive integer, smaller or equal to --@(windowsize).\nDefault is @(DEFAULT)."};
$PARAM{temperature} = {key => 'temperature', gapc => 'T', type => 'f', default => 37, info => "Rescale energy parameters to a temperature of temp C.\n<float> must be a floating point number.\nDefault is @(DEFAULT) C."};
$PARAM{param} = {key => 'param', gapc => 'P', type => 's', default => undef, infoType => "paramfile", info => "Read energy parameters from paramfile, instead of using the default parameter set. See the RNAlib (Vienna RNA package) documentation for details on the file format.\nDefault are parameters released by the Turner group in 2004 (see [5] and [6])."};
$PARAM{minhairpinlength} = {key => 'minHairpinLength', gapc => 'z', type => 'i', default => 2, info => "Set minimal hairpin length for K-type pseudoknots. The first heuristic step in computung kissing hairpins, is to find stable, non-interrupted helices. These helices must have a minimal length, i.e. number of stacked base-pairs, of <int>. The higher the value, the faster the program, but also the less accurate. This affects only the stems of both hairpins, not their kissing helix!\n<int> must be a positive number.\nDefault is @(DEFAULT)."};
$PARAM{strategy} = {key => 'strategy', gapc => 's', type => 's', default => 'A', infoType => "char", info => "Select pseudoknot strategy. There are four different strategies how to compute kissing hairpins (K-type pseudoknots). We suggest 'A', see [7] for details. If you choose 'P' only H-type pseudoknots can be computed.\nAvailable strategies are 'A','B','C','D' and 'P'. On input omit the ticks.\nDefault is '@(DEFAULT)'."};
$PARAM{maxknotsize} = {key => 'maxKnotSize', gapc => 'l', type => 'i', default => undef, info => "Set a maximal pseudoknot size. To speed up computation, you can limit the number of bases involved in a pseudoknot (and all it's loop regions) by giving <int>.\nOnly positive numbers are allowed for <int>\nBy default, there is no limitation, i.e. --@(maxknotsize) is set to input length."};
$PARAM{hpenalty} = {key => 'Hpenalty', gapc => 'x', type => 'f', default => +9.00, info => "Set init. energy penalty for an H-type pseudoknot. Thermodynamic energy parameters for pseudoknots have not been measured in a wet lab, yet. Thus, you might want to set the penalty for opening a H-type pseudoknot yourself.\n<float> must be a floating point number.\nDefault is @(DEFAULT) kcal/mol."};
$PARAM{kpenalty} = {key => 'Kpenalty', gapc => 'y', type => 'f', default => +12.00, info => "Set init. energy penalty for an K-type pseudoknot. Thermodynamic energy parameters for pseudoknots have not been measured in a wet lab, yet. Thus, you might want to set the penalty for opening a K-type pseudoknot yourself.\n<float> must be a floating point number.\nDefault is @(DEFAULT) kcal/mol."};
$PARAM{allowlp} = {key => 'allowLP', gapc => 'u', type => 'i', default => 0, info => "Lonely base pairs have no stabilizing effect, because they cannot stack on another pair, but they heavily increase the size of the folding space. Thus, we normally forbid them. Should you want to allow them set <int> to 1.\n<int> must be 0 (=don't allow lonely base pairs) or 1 (= allow them).\nDefault is @(DEFAULT), i.e. no lonely base pairs."};
$PARAM{absolutedeviation} = {key => 'absoluteDeviation', gapc => 'e', type => 'f', default => undef, info => "This sets the energy range as an absolute value of the minimum free energy. For example, when --@(absolutedeviation) 10.0 is specified, and the minimum free energy is -10.0 kcal/mol, the energy range is set to 0.0 to -10.0 kcal/mol.\n<float> must be a positive floating point number.\nConnot be combined with --@(relativedeviation).\nFor modes: \"$MODE_SUBOPT\", \"$MODE_LOCAL\", \"$MODE_SHAPES\"."};
$PARAM{relativedeviation} = {key => 'relativeDeviation', gapc => 'c', type => 'f', default => 10.0, info => "This sets the energy range as percentage value of the minimum free energy. For example, when --@(relativedeviation) 5.0 is specified, and the minimum free energy is -10.0 kcal/mol, the energy range is set to -9.5 to -10.0 kcal/mol.\n<float> must be a positive floating point number.\nBy default, --@(relativedeviation) is set to @(DEFAULT) %.\nCannot be combined with --@(absolutedeviation).\nFor modes: \"$MODE_SUBOPT\", \"$MODE_LOCAL\", \"$MODE_SHAPES\"."};
$PARAM{shapelevel} = {key => 'shapeLevel', gapc => 'q', type => 'i', default => 5, info => "Set shape abstraction level. Currently, we provide five different levels (see [1] for their definitions), where 5 is the most abstract and 1 the most concrete one.\n<int> must be a number between 5 and 1.\nDefault is @(DEFAULT) (the most abstract one).\nFor modes: \"$MODE_SHAPES\", \"$MODE_PROBS\"."};
$PARAM{lowprobfilter} = {key => 'lowProbFilter', gapc => 'F', type => 'f', default => 0.000001, info => "This option sets a barrier for filtering out results with very low probabilities during calculation. The default value here is @(DEFAULT), which gives a significant speedup compared to a disabled filter. (See [4] for details.) Note that this filter can have a slight influence on the overall results. To disable this filter, use option --@(lowprobfilter) 0. \n<float> must be a positive floating point number smaller than 1.\nFor mode: \"$MODE_PROBS\"."};
$PARAM{help} = {key => 'help', default => undef, info => "show this brief help on version and usage"};
$PARAM{binarypath} = {key => 'binPath', type => 's', default => undef, info => "$0 expects that according Bellman's GAP compiled binaries are located in the same directory as the Perl wrapper is. Should you moved them into another directory, you must set --@(binarypath) to this new location!"};
$PARAM{binaryprefix} = {key => 'binPrefix', type => 's', default => 'pKiss_', info => "$0 expects a special naming schema for the according Bellman's GAP compiled binaries. On default, each binary is named \"@(DEFAULT)\", followed by the mode, followed by \"_window\" for the window mode version. Thus, for non-window mode \"$MODE_SUBOPT\" the name would be \"@(DEFAULT)$MODE_SUBOPT\". With --@(binaryprefix) you can change the prefix into some arbitary one."};
$PARAM{probdecimals} = {key => 'probDecimals', type => 'i', default => 7, info => "Sets the number of digits used for printing shape probabilities.\n<int> must be a positive integer number.\nDefault is @(DEFAULT)."};

our $OUTPUT_MINLEFTWIDTH = 7;
our $OUTPUT_FIELDSPACER = "  ";

my $settings = {};
foreach my $param (keys %PARAM) {
	$settings->{$param} = $PARAM{$param}->{default};
}
my %help = ();
foreach my $param (keys %PARAM) {
	my $optionSec = $PARAM{$param}->{key};
	$optionSec .= "=".$PARAM{$param}->{type} if (exists $PARAM{$param}->{type});
	$help{$optionSec} = \$settings->{$param};
}

&GetOptions( 	
	%help
);

#~ print Dumper printParamUsage($PARAM{relativedeviation});
#~ die;
my $sequenceNumber = 0;
checkParameters($settings);

if (@ARGV == 0) {
	if (defined $settings->{'help'}) {
		usage();
	} else {
		if (isatty(*STDIN)) {
			print "waiting for your plain RNA sequence or fasta filename.\n";
			my $input = <STDIN>; chomp $input;
			if (-e $input) {
				Utils::applyFunctionToFastaFile($input, \&doComputation, $settings);
			} else {
				my %sequence = ("header", "unnamed sequence 1", "sequence", $input);
				doComputation(\%sequence, $settings);
			}
		} else {
			Utils::applyFunctionToFastaFile(\*STDIN, \&doComputation, $settings);
		}
	}
} else {
	usage() if ((defined $settings->{'help'}) || (@ARGV > 1));
	my ($input) = @ARGV;
	if (-e $input) {
		Utils::applyFunctionToFastaFile($input, \&doComputation, $settings);
	} else {
		my %sequence = ("header", "unnamed sequence 1", "sequence", $input);
		doComputation(\%sequence, $settings);
	}
}

sub doComputation {
	my ($refHash_sequence, $settings) = @_;
	
	if ($refHash_sequence->{sequence} !~ m/^\s*((A|C|G|U|T)+)\s*$/i) {
		print STDERR "sequence '".$refHash_sequence->{header}."' has been skipped, due to non RNA letter. Only A,C,G,U,T,a,c,g,u,t are allowed.";
	}
	my $seq = $refHash_sequence->{sequence};
	$seq =~ s/t/u/gi;
	my $command = buildCommand($settings, $refHash_sequence);
	my $result = qx($command "$seq");
	
	print "\n" if ($sequenceNumber != 0);
	if (($settings->{'mode'} eq $MODE_SUBOPT) || ($settings->{'mode'} eq $MODE_MFE)) {
		parseSubopt($result, $settings, $refHash_sequence);
	} elsif ($settings->{'mode'} eq $MODE_ENFORCE) {
		parseEnforce($result, $settings, $refHash_sequence);
	} elsif ($settings->{'mode'} eq $MODE_LOCAL) {
		parseLocal($result, $settings, $refHash_sequence);
	} elsif ($settings->{'mode'} eq $MODE_SHAPES) {
		parseShapes($result, $settings, $refHash_sequence);
	} elsif ($settings->{'mode'} eq $MODE_PROBS) {
		parseProbs($result, $settings, $refHash_sequence);
	} else {
		print $result;
	}
	
	$sequenceNumber++;
	return undef;
}

sub parseSubopt {
	my ($result, $settings, $refHash_sequence) = @_;
	
	my %predictions = ();
	my $maxEnergyLen = $OUTPUT_MINLEFTWIDTH;
	my $maxStartPosLen = 0;
	my $windowStartPos = undef;
	my $windowEndPos = undef;
	foreach my $line (split(m/\r?\n/, $result)) {
		if ($line =~ m/^Answer\s*\((\d+), (\d+)\)\s+:\s*$/) {
			($windowStartPos, $windowEndPos) = ($1, $2);
		} elsif ($line =~ m/^Answer:\s*$/) {
			($windowStartPos, $windowEndPos) = (0, length($refHash_sequence->{sequence}));
		} elsif ($line =~ m/^\s*$/) {
		} else {
			my ($energy, $structure) = ($line =~ m/\( (.+?) , (.+?) \)/);
			$energy = sprintf("%.2f", $energy/100);
			if (not exists $predictions{$windowStartPos."-".$windowEndPos}->{$structure}) {
				$predictions{$windowStartPos."-".$windowEndPos}->{$structure} = $energy;
			} else {
				$predictions{$windowStartPos."-".$windowEndPos}->{$structure} = $energy if ($energy < $predictions{$windowStartPos."-".$windowEndPos}->{$structure});
			}
			$maxEnergyLen = length($energy) if ($maxEnergyLen < length($energy));
			$maxStartPosLen = length($windowStartPos) if ($maxStartPosLen < length($windowStartPos));
		}
	}

	my $leftLength = $maxEnergyLen;
	$leftLength = $maxStartPosLen if ($leftLength < $maxStartPosLen);

	print ">".$refHash_sequence->{header}."\n";
	my @windowPositions = sort {getStartPos($a) <=> getStartPos($b)} keys(%predictions);
	foreach my $windowPos (@windowPositions) {
		my ($startPos, $endPos) = split(m/-/, $windowPos);
		print sprintf("% ${leftLength}i", $startPos+1).$OUTPUT_FIELDSPACER.substr($refHash_sequence->{sequence}, $startPos, $endPos-$startPos).$OUTPUT_FIELDSPACER.($endPos)."\n";
		foreach my $structure (sort {$predictions{$windowPos}->{$a} <=> $predictions{$windowPos}->{$b}} keys(%{$predictions{$windowPos}})) {
			my $energy = $predictions{$windowPos}->{$structure};
			print sprintf("% $leftLength.2f", $energy).$OUTPUT_FIELDSPACER.$structure."\n"
		}
		print "\n" if ($windowPos ne $windowPositions[$#windowPositions]);
	}
}

sub parseProbs {
	my ($result, $settings, $refHash_sequence) = @_;

	my %predictions = ();
	my $maxEnergyLen = $OUTPUT_MINLEFTWIDTH;
	my $maxStartPosLen = 0;
	my $windowStartPos = undef;
	my $windowEndPos = undef;
	my %sumProbs = ();
	foreach my $line (split(m/\r?\n/, $result)) {
		if ($line =~ m/^Answer\s*\((\d+), (\d+)\)\s+:\s*$/) {
			($windowStartPos, $windowEndPos) = ($1, $2);
		} elsif ($line =~ m/^Answer:\s*$/) {
			($windowStartPos, $windowEndPos) = (0, length($refHash_sequence->{sequence}));
		} elsif ($line =~ m/^\s*$/) {
		} else {
			my ($class, $energy, $probability, $structure) = ($line =~ m/\( \( (.+?) , \( (.+?) , (.+?) \) \) , (.+?) \)/);
			$energy = sprintf("%.2f", $energy/100);
			$sumProbs{$windowStartPos."-".$windowEndPos} += $probability;
			$maxEnergyLen = length($energy) if ($maxEnergyLen < length($energy));
			$maxStartPosLen = length($windowStartPos) if ($maxStartPosLen < length($windowStartPos));
			$predictions{$windowStartPos."-".$windowEndPos}->{$probability}->{$energy}->{$class}->{$structure}++;
		}
	}

	my $leftLength = $maxEnergyLen;
	$leftLength = $maxStartPosLen if ($leftLength < $maxStartPosLen);

	print ">".$refHash_sequence->{header}."\n";
	my @windowPositions = sort {getStartPos($a) <=> getStartPos($b)} keys(%predictions);
	my $numDecProb = $settings->{'probdecimals'};

	foreach my $windowPos (@windowPositions) {
		my ($startPos, $endPos) = split(m/-/, $windowPos);
		print sprintf("% ${leftLength}i", $startPos+1).$OUTPUT_FIELDSPACER.substr($refHash_sequence->{sequence}, $startPos, $endPos-$startPos).$OUTPUT_FIELDSPACER.($endPos)."\n";
		foreach my $probability (sort {$b <=> $a} keys(%{$predictions{$windowPos}})) {
			foreach my $energy (sort {$a <=> $b} keys(%{$predictions{$windowPos}->{$probability}})) {
				foreach my $class (sort {$a cmp $b} keys(%{$predictions{$windowPos}->{$probability}->{$energy}})) {
					foreach my $structure (sort {$a cmp $b} keys(%{$predictions{$windowPos}->{$probability}->{$energy}->{$class}})) {
						print sprintf("% $leftLength.2f", $energy).$OUTPUT_FIELDSPACER.$structure.$OUTPUT_FIELDSPACER.sprintf("%1.${numDecProb}f", $probability/$sumProbs{$windowPos}).$OUTPUT_FIELDSPACER.$class."\n";
					}
				}
			}
		}
		print "\n" if ($windowPos ne $windowPositions[$#windowPositions]);
	}
}

sub parseShapes {
	my ($result, $settings, $refHash_sequence) = @_;

	my %predictions = ();
	my $maxEnergyLen = $OUTPUT_MINLEFTWIDTH;
	my $maxStartPosLen = 0;
	my $windowStartPos = undef;
	my $windowEndPos = undef;
	foreach my $line (split(m/\r?\n/, $result)) {
		if ($line =~ m/^Answer\s*\((\d+), (\d+)\)\s+:\s*$/) {
			($windowStartPos, $windowEndPos) = ($1, $2);
		} elsif ($line =~ m/^Answer:\s*$/) {
			($windowStartPos, $windowEndPos) = (0, length($refHash_sequence->{sequence}));
		} elsif ($line =~ m/^\s*$/) {
		} else {
			my ($class, $energy, $structure) = ($line =~ m/\( \( (.+?) , (.+?) \) , (.+?) \)/);
			$energy = sprintf("%.2f", $energy/100);
			$maxEnergyLen = length($energy) if ($maxEnergyLen < length($energy));
			$maxStartPosLen = length($windowStartPos) if ($maxStartPosLen < length($windowStartPos));			
			$predictions{$windowStartPos."-".$windowEndPos}->{$energy}->{$class}->{$structure}++;
		}
	}

	my $leftLength = $maxEnergyLen;
	$leftLength = $maxStartPosLen if ($leftLength < $maxStartPosLen);

	print ">".$refHash_sequence->{header}."\n";
	my @windowPositions = sort {getStartPos($a) <=> getStartPos($b)} keys(%predictions);
	foreach my $windowPos (@windowPositions) {
		my ($startPos, $endPos) = split(m/-/, $windowPos);
		print sprintf("% ${leftLength}i", $startPos+1).$OUTPUT_FIELDSPACER.substr($refHash_sequence->{sequence}, $startPos, $endPos-$startPos).$OUTPUT_FIELDSPACER.($endPos)."\n";
		foreach my $energy (sort {$a <=> $b} keys(%{$predictions{$windowPos}})) {
			foreach my $class (sort {$a cmp $b} keys(%{$predictions{$windowPos}->{$energy}})) {
				foreach my $structure (sort {$a cmp $b} keys(%{$predictions{$windowPos}->{$energy}->{$class}})) {
					print sprintf("% $leftLength.2f", $energy).$OUTPUT_FIELDSPACER.$structure.$OUTPUT_FIELDSPACER.$class."\n";
				}
			}
		}
		print "\n" if ($windowPos ne $windowPositions[$#windowPositions]);
	}
}

sub parseLocal {
	my ($result, $settings, $refHash_sequence) = @_;
	
	my %predictions = ();
	my $maxEnergyLen = $OUTPUT_MINLEFTWIDTH;
	my $maxStartPosLen = 0;
	my $windowStartPos = undef;
	my $windowEndPos = undef;
	foreach my $line (split(m/\r?\n/, $result)) {
		if ($line =~ m/^Answer\s*\((\d+), (\d+)\)\s+:\s*$/) {
			($windowStartPos, $windowEndPos) = ($1, $2);
		} elsif ($line =~ m/^Answer:\s*$/) {
			($windowStartPos, $windowEndPos) = (0, length($refHash_sequence->{sequence}));
		} elsif ($line =~ m/^\s*$/) {
		} else {
			my ($energy, $startPos, $structure, $endPos) = ($line =~ m/\( (.+?) , (\d+) (.+?) (\d+) \)/);
			$energy = sprintf("%.2f", $energy/100);
			if (not exists $predictions{$windowStartPos."-".$windowEndPos}->{$startPos}->{$endPos}->{$structure}) {
				$predictions{$windowStartPos."-".$windowEndPos}->{$startPos}->{$endPos}->{$structure} = $energy;
			} else {
				$predictions{$windowStartPos."-".$windowEndPos}->{$startPos}->{$endPos}->{$structure} = $energy if ($energy < $predictions{$windowStartPos."-".$windowEndPos}->{$startPos}->{$endPos}->{$structure});
			}
			$maxEnergyLen = length($energy) if ($maxEnergyLen < length($energy));
			$maxStartPosLen = length($startPos) if ($maxStartPosLen < length($startPos));
			$maxStartPosLen = length($windowStartPos) if ($maxStartPosLen < length($windowStartPos));
		}
	}
	
	my $leftLength = $maxEnergyLen;
	$leftLength = $maxStartPosLen if ($leftLength < $maxStartPosLen);

	my %blocks = ();
	foreach my $windowPos (keys(%predictions)) {
		foreach my $startPos (sort {$a <=> $b} keys(%{$predictions{$windowPos}})) {
			foreach my $endPos (sort {$a <=> $b} keys(%{$predictions{$windowPos}->{$startPos}})) {
				my $block = "";
				my $mfe = 0;
				$block .= sprintf("% ${leftLength}i", $startPos).$OUTPUT_FIELDSPACER.substr($refHash_sequence->{sequence}, $startPos-1, $endPos-$startPos).$OUTPUT_FIELDSPACER.($endPos-1)."\n";
				foreach my $structure (sort {$predictions{$windowPos}->{$startPos}->{$endPos}->{$a} <=> $predictions{$windowPos}->{$startPos}->{$endPos}->{$b}} keys(%{$predictions{$windowPos}->{$startPos}->{$endPos}})) {
					my $energy = $predictions{$windowPos}->{$startPos}->{$endPos}->{$structure};
					$block .= sprintf("% $leftLength.2f", $energy).$OUTPUT_FIELDSPACER.$structure."\n";
					$mfe = $energy if ($energy < $mfe);
				}
				push @{$blocks{$windowPos}}, {block => $block, mfe => $mfe, startPos => $startPos, endPos => $endPos};
			}
		}
	}

	print ">".$refHash_sequence->{header}."\n";
	my @windowPositions = sort {getStartPos($a) <=> getStartPos($b)} keys(%predictions);
	foreach my $windowPos (@windowPositions) {
		my ($startPos, $endPos) = split(m/-/, $windowPos);
		print "=== window: ".($startPos+1)." to ".$endPos.": ===\n";
		my @blocks = sort {($a->{mfe} <=> $b->{mfe}) || ($a->{startPos} <=> $b->{startPos}) || ($a->{endPos} <=> $b->{endPos})} @{$blocks{$windowPos}};
		foreach my $block (@blocks) {
			print $block->{block};
			print "\n" if ($block != $blocks[$#blocks]);
		}
		print "\n" if ($windowPos ne $windowPositions[$#windowPositions]);
	}
}

sub parseEnforce {
	my ($result, $settings, $refHash_sequence) = @_;
	
	my %predictions = ();
	my $maxEnergyLen = $OUTPUT_MINLEFTWIDTH;
	my $maxStartPosLen = 0;
	my $windowStartPos = undef;
	my $windowEndPos = undef;
	foreach my $line (split(m/\r?\n/, $result)) {
		if ($line =~ m/^Answer\s*\((\d+), (\d+)\)\s+:\s*$/) {
			($windowStartPos, $windowEndPos) = ($1, $2);
		} elsif ($line =~ m/^Answer:\s*$/) {
			($windowStartPos, $windowEndPos) = (0, length($refHash_sequence->{sequence}));
		} elsif ($line =~ m/^\s*$/) {
		} else {
			my ($class, $energy, $structure) = ($line =~ m/\( \( (.+?) , (.+?) \) , (.+?) \)/);
			$energy = sprintf("%.2f", $energy/100);
			$maxEnergyLen = length($energy) if ($maxEnergyLen < length($energy));
			$maxStartPosLen = length($windowStartPos) if ($maxStartPosLen < length($windowStartPos));			
			$predictions{$windowStartPos."-".$windowEndPos}->{$class}->{$energy}->{$structure}++;
		}
	}
	
	my $leftLength = $maxEnergyLen;
	$leftLength = $maxStartPosLen if ($leftLength < $maxStartPosLen);

	my $notAvail = "no structure available";
	print ">".$refHash_sequence->{header}."\n";
	my @windowPositions = sort {getStartPos($a) <=> getStartPos($b)} keys(%predictions);
	foreach my $windowPos (@windowPositions) {
		my ($startPos, $endPos) = split(m/-/, $windowPos);
		print sprintf("% ${leftLength}i", $startPos+1).$OUTPUT_FIELDSPACER.substr($refHash_sequence->{sequence}, $startPos, $endPos-$startPos).$OUTPUT_FIELDSPACER.($endPos)."\n";
		foreach my $class ("nested structure","H-type pseudoknot","K-type pseudoknot","H- and K-type pseudoknot") {
			if (not exists $predictions{$windowPos}->{$class}) {
				print "".(" " x $maxEnergyLen).$OUTPUT_FIELDSPACER.$notAvail.(" " x (($endPos-$startPos)-length($notAvail))).$OUTPUT_FIELDSPACER."best '".$class."'\n";
			} else {
				foreach my $energy (sort {$a <=> $b} keys(%{$predictions{$windowPos}->{$class}})) {
					foreach my $structure (sort {$a cmp $b} keys(%{$predictions{$windowPos}->{$class}->{$energy}})) {
						print sprintf("% $leftLength.2f", $energy).$OUTPUT_FIELDSPACER.$structure.(" " x (length($notAvail) - length($structure))).$OUTPUT_FIELDSPACER."best '".$class."'\n"
					}
				}
			}
		}
		print "\n" if ($windowPos ne $windowPositions[$#windowPositions]);
	}
}

sub buildCommand {
	my ($settings, $refHash_sequence) = @_;
	
	my $cmd = "";
	$cmd .= $settings->{'binarypath'};
	$cmd .= "/" if (substr($cmd, -1, 1) ne "/");
	$cmd .= $settings->{'binaryprefix'}.$settings->{'mode'};
	if (defined $settings->{'windowsize'}) {
		$cmd .= "_window";
		my $windowSize = $settings->{'windowsize'};
		$windowSize = length($refHash_sequence->{sequence}) if ($settings->{'windowsize'} > length($refHash_sequence->{sequence}));
		$cmd .= " -".$PARAM{windowsize}->{gapc}." ".$windowSize;
		$cmd .= " -".$PARAM{windowincrement}->{gapc}." ".$settings->{'windowincrement'};
	}
	$cmd .= " -".$PARAM{temperature}->{gapc}." ".$settings->{'temperature'} if ($settings->{'temperature'} != $PARAM{temperature}->{default});
	$cmd .= " -".$PARAM{param}->{gapc}." ".$settings->{'param'} if (defined $settings->{'param'});
	$cmd .= " -".$PARAM{minhairpinlength}->{gapc}." ".$settings->{'minhairpinlength'} if ($settings->{'minhairpinlength'} != $PARAM{minhairpinlength}->{default});
	$cmd .= " -".$PARAM{strategy}->{gapc}." ".$settings->{'strategy'} if (uc($settings->{'strategy'}) ne uc($PARAM{strategy}->{default}));
	$cmd .= " -".$PARAM{maxknotsize}->{gapc}." ".$settings->{'maxknotsize'} if (defined $settings->{'maxknotsize'});
	$cmd .= " -".$PARAM{hpenalty}->{gapc}." ".$settings->{'hpenalty'} if ($settings->{'hpenalty'} != $PARAM{hpenalty}->{default});
	$cmd .= " -".$PARAM{kpenalty}->{gapc}." ".$settings->{'kpenalty'} if ($settings->{'kpenalty'} != $PARAM{kpenalty}->{default});
	$cmd .= " -".$PARAM{allowlp}->{gapc}." ".$settings->{'allowlp'} if ($settings->{'allowlp'} != $PARAM{allowlp}->{default});
	$cmd .= " -".$PARAM{relativedeviation}->{gapc}." ".$settings->{'relativedeviation'} if ($settings->{'relativedeviation'} != $PARAM{relativedeviation}->{default});
	$cmd .= " -".$PARAM{absolutedeviation}->{gapc}." ".$settings->{'absolutedeviation'} if (defined $settings->{'absolutedeviation'});
	$cmd .= " -".$PARAM{shapelevel}->{gapc}." ".$settings->{'shapelevel'} if ($settings->{'shapelevel'} != $PARAM{shapelevel}->{default});
	$cmd .= " -".$PARAM{lowprobfilter}->{gapc}." ".$settings->{'lowprobfilter'} if ($settings->{'lowprobfilter'} != $PARAM{lowprobfilter}->{default});

	return $cmd;
}

sub checkParameters {
	my ($settings) = @_;
	
	my $diePrefix = "wrong command line parameter:\n  ";
	
	if (
		($settings->{'mode'} ne $MODE_MFE) && 
		($settings->{'mode'} ne $MODE_SUBOPT) &&
		($settings->{'mode'} ne $MODE_ENFORCE) && 
		($settings->{'mode'} ne $MODE_LOCAL) && 
		($settings->{'mode'} ne $MODE_SHAPES) &&
		($settings->{'mode'} ne $MODE_PROBS)) {
			die $diePrefix."mode '".$settings->{'mode'}."' is not available. Please choose one out of \"".join('", "', ($MODE_MFE, $MODE_SUBOPT, $MODE_ENFORCE, $MODE_LOCAL, $MODE_SHAPES, $MODE_PROBS))."\".\n";
	}
	die $diePrefix."--".$PARAM{'windowsize'}->{key}." must be a positive integer!\n" if ((defined $settings->{'windowsize'}) && ($settings->{'windowsize'} < 1));
	die $diePrefix."--".$PARAM{'windowsize'}->{key}." is smaller than --".$PARAM{'windowincrement'}->{key}." !\n" if ((defined $settings->{'windowsize'}) && ($settings->{'windowsize'} < $settings->{'windowincrement'}));
	die $diePrefix."the parameter file you specified could not be found.\n" if ((defined $settings->{'param'}) && (not -e $settings->{'param'}));
	die $diePrefix."--".$PARAM{'minhairpinlength'}->{key}." must be a positive integer.\n" if ($settings->{'minhairpinlength'} <= 0);
	die $diePrefix."there is no strategy '".$PARAM{'strategy'}->{key}."'. Please select one of 'A', 'B', 'C', 'D', 'P'.\n" if ($settings->{'strategy'} !~ m/^A|B|C|D|P$/i);
	die $diePrefix."--".$PARAM{'maxknotsize'}->{key}." must be a positive integer.\n" if ((defined $settings->{'maxknotsize'}) && ($settings->{'maxknotsize'} <= 0));
	die $diePrefix."--".$PARAM{'allowlp'}->{key}." can either be 0 or 1, to forbid or disallow lonely base pairs.\n" if ($settings->{'allowlp'} !~ m/^0|1$/);
	die $diePrefix."--".$PARAM{'shapelevel'}->{key}." must be a number between 5 and 1.\n" if (($settings->{'shapelevel'} < 1) || ($settings->{'shapelevel'} > 5));
	die $diePrefix."--".$PARAM{'lowprobfilter'}->{key}." must be a positive floating point number below 1.\n" if (($settings->{'lowprobfilter'} >= 1) || ($settings->{'lowprobfilter'} < 0));

	die $diePrefix."--".$PARAM{'absolutedeviation'}->{key}." cannot be used in mode '".$PARAM{'mode'}->{key}."'!\n" if ((defined $settings->{'absolutedeviation'}) && (($settings->{'mode'} ne $MODE_SUBOPT) && ($settings->{'mode'} ne $MODE_LOCAL) && ($settings->{'mode'} ne $MODE_SHAPES)));
	die $diePrefix."--".$PARAM{'relativedeviation'}->{key}." cannot be used in mode '".$PARAM{'mode'}->{key}."'!\n" if (($settings->{'relativedeviation'} != $PARAM{'relativedeviation'}->{default}) && (($settings->{'mode'} ne $MODE_SUBOPT) && ($settings->{'mode'} ne $MODE_LOCAL) && ($settings->{'mode'} ne $MODE_SHAPES)));
	die $diePrefix."--".$PARAM{'shapelevel'}->{key}." cannot be used in mode '".$PARAM{'mode'}->{key}."'!\n" if (($settings->{'shapelevel'} != $PARAM{'shapelevel'}->{default}) && (($settings->{'mode'} ne $MODE_SHAPES) && ($settings->{'mode'} ne $MODE_PROBS)));
	die $diePrefix."--".$PARAM{'lowprobfilter'}->{key}." cannot be used in mode '".$PARAM{'mode'}->{key}."'!\n" if (($settings->{'lowprobfilter'} != $PARAM{'lowprobfilter'}->{default}) && ($settings->{'mode'} ne $MODE_PROBS));
	
	die $diePrefix."--".$PARAM{'absolutedeviation'}->{key}." and --".$PARAM{'relativedeviation'}->{key}." cannot be set at the same time!\n" if ((defined $settings->{'absolutedeviation'}) && ($settings->{'relativedeviation'} != $PARAM{'relativedeviation'}->{default}));
	
	my ($programPath, $programName) = @{Utils::separateDirAndFile($0)};
	$programPath = "./" if (not defined $programPath);
	$settings->{'binarypath'} = $programPath if (not defined $settings->{'binarypath'});
	my $binName = "";
	if (defined $settings->{'binarypath'}) {
		$binName .= $settings->{'binarypath'};
		$binName .= "/" if (substr($binName, -1, 1) ne "/");
	} else {
		$binName .= "./";
	}
	$binName .= $settings->{'binaryprefix'}.$settings->{'mode'};
	die $diePrefix." could not find Bellman's GAP binary '".$binName."' for mode ".$settings->{'mode'}."!\n" if (not -e $binName);
	die $diePrefix." could not find window mode for Bellman's GAP binary '".$binName."_window' for mode ".$settings->{'mode'}."!\n" if ((not -e $binName."_window") && (defined $settings->{'windowsize'}));
	
	die $diePrefix."--".$PARAM{'probdecimals'}->{key}." must be a positive integer number!\n" if ($settings->{'probdecimals'} < 0);
}



sub usage {
	my ($settings) = @_;

my $HELP = <<EOF;
# pKiss: RNA secondary structure predictions including pseudoknots
#        version 2.0.10 (03.02.2013)
#        Stefan Janssen (bibi-help\@techfak.uni-bielefeld.de)
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

USAGE: 
perl $0 [-mode] [-options] <fasta file name or RNA sequence>

 pKiss comes with the following different modes of predictions:
EOF
;
	$HELP .= Utils::printIdent("  ".$MODE_MFE."     : ", Utils::usage_convertInfoText("Computes the single energetically most stable secondary structure for the given RNA sequence. This structure might contain a pseudoknot of type H (simple canonical recursive pseudoknot) or type K (simple canonical recursive kissing hairpin), but need not to. Co-optimal results will be suppressed, i.e. should different prediction have the same best energy value, just an arbitrary one out of them will be reported.", \%PARAM))."\n";
	$HELP .= Utils::printIdent("  ".$MODE_SUBOPT."  : ", Utils::usage_convertInfoText("Often, the biological relevant structure is hidden among suboptimal predictions. In \"$MODE_SUBOPT mode\", you can also inspect all suboptimal solutions up to a given threshold (see parameters --@(absolutedeviation) and --@(relativedeviation)). Due to semantic ambiguity of the underlying \"microstate\" grammar, sometimes identical predictions will show up. As Vienna-Dot-Bracket strings they seem to be the same, but according to base dangling they differ and thus might even have slightly different energies. See [1] for details.", \%PARAM))."\n";
	$HELP .= Utils::printIdent("  ".$MODE_ENFORCE." : ", Utils::usage_convertInfoText("Energetically best pseudoknots might be deeply buried under suboptimal solutions. Use \"$MODE_ENFORCE\" mode to enforce a structure prediction for each of the for classes: \"nested structure\" (as \"RNAfold\" would compute, i.e. without pseudoknots), \"H-type pseudoknot\", \"K-type pseudoknot\" and \"H- and K-type pseudoknot\". Useful if you want to compute the tendency of folding a pseudoknot or not, like in [2].", \%PARAM))."\n";
	$HELP .= Utils::printIdent("  ".$MODE_LOCAL."   : ", Utils::usage_convertInfoText("Computes energetically best and suboptimal local pseudoknots. Local means, leading and trailing bases can be omitted and every prediction is a pseudoknot.", \%PARAM))."\n";
	$HELP .= Utils::printIdent("  ".$MODE_SHAPES."  : ", Utils::usage_convertInfoText("Output of \"$MODE_SUBOPT\" mode is crowded by many very similar answers, which make it hard to focus to the \"important\" changes. The abstract shape concept [3] groups similar answers together and reports only the best answer within such a group. Due to abstraction, suboptimal analyses can be done more thorough, by ignoring boring differences.\n(see parameter --@(shapelevel))", \%PARAM))."\n";
	$HELP .= Utils::printIdent("  ".$MODE_PROBS."   : ", Utils::usage_convertInfoText("Structure probabilities are strictly correlated to their energy values. Grouped together into shape classes, their probabilities add up. Often a shape class with many members of worse energy becomes more probable than the shape containing the mfe structure but not much more members. See [4] for details on shape probabilities.", \%PARAM))."\n";
	$HELP .= "GENERAL OPTIONS:\n";
	for my $par ('help','mode','windowsize','windowincrement','temperature','param','minhairpinlength','strategy','maxknotsize','hpenalty','kpenalty','allowlp') {
		$HELP .= Utils::printParamUsage($PARAM{$par}, \%PARAM)."\n";
	}
	$HELP .= "OPTIONS ONLY VALID FOR SOME MODES:\n";
	for my $par ('absolutedeviation','relativedeviation','shapelevel','lowprobfilter') {
		$HELP .= Utils::printParamUsage($PARAM{$par}, \%PARAM)."\n";
	}
	$HELP .= "SYSTEM DEPENDENT OPTIONS:\n";
	for my $par ('binarypath','binaryprefix','probdecimals') {
		$HELP .= Utils::printParamUsage($PARAM{$par}, \%PARAM)."\n";
	}
	$HELP .= "REFERENCES:\n";
	$HELP .= Utils::printIdent("[1] ", "Stefan Janssen, Christian Schudoma, Gerhard Steger, Robert Giegerich.\n\"Lost in folding space? Comparing four variants of the thermodynamic model for RNA secondary structure prediction.\"\nBMC Bioinformatics 2011. doi:10.1186/1471-2105-12-429")."\n";
	$HELP .= Utils::printIdent("[2] ", "Corinna Theis, Jens Reeder, Robert Giegerich.\n\"KnotInFrame: prediction of -1 ribosomal frameshift events.\"\nNucleic Acids Research 2008. doi:10.1093/nar/gkn578")."\n";
	$HELP .= Utils::printIdent("[3] ", "Stefan Janssen, Robert Giegerich.\n\"Faster computation of exact RNA shape probabilities.\"\nBioinformatics 2010. doi:10.1093/bioinformatics/btq014")."\n";
	$HELP .= Utils::printIdent("[4] ", "Bjoern Voss, Robert Giegerich, Marc Rehmsmeier.\n\"Complete probabilistic analysis of RNA shapes.\"\nBMC Biology 2006. doi:10.1186/1741-7007-4-5")."\n";
	$HELP .= Utils::printIdent("[5] ", "David H Mathews, Matthew D Disney, Jessica L Childs, Susan J Schroeder, Michael Zuker, Douglas H Turner.\n\"Incorporating chemical modification constraints into a dynamic programming algorithm for prediction of RNA secondary structure.\"\nProceedings of the National Academy of Sciences of the United States of America 2004. doi: 10.1073/pnas.0401799101")."\n";
	$HELP .= Utils::printIdent("[6] ", "Douglas H Turner, David H Mathews.\n\"NNDB: The nearest neighbor parameter database for predicting stability of nucleic acid secondary structure.\"\"Nucleic Acids Research 2009. doi:10.1093/nar/gkp892")."\n";
	$HELP .= "CITATION:\n    If you use this program in your work you might want to cite:\n\n";
	$HELP .= Utils::printIdent("[7] ", "Corinna Theis, Stefan Janssen, Robert Giegerich.\n\"Prediction of RNA secondary structure including kissing hairpin motifs.\"\nAlgorithms in Bioinformatics 2010. doi:10.1007/978-3-642-15294-8_5")."\n";

	print $HELP;
	exit(0);
}

sub getStartPos {
	my ($windowPos) = @_;
	my ($startPos, $endPos) = split(m/-/, $windowPos);
	return $startPos;
}

